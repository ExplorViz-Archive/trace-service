plugins {
  id 'java'
  id 'io.quarkus'
  id 'com.adarshr.test-logger' version '2.1.0'
  id "com.commercehub.gradle.plugin.avro" version "0.21.0"
  id 'eclipse'
}

apply from: 'buildscripts/code-analysis.gradle'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://packages.confluent.io/maven/"
  }
}

sourceSets {
  integrationTest {
    java.srcDir file('src/integrationTest/java')
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

eclipse {
  classpath {
    plusConfigurations.add configurations.integrationTestRuntimeOnly
    plusConfigurations.add configurations.integrationTestImplementation
  }
}

task integrationTest(type: Test) {
  description = 'Runs integration tests.'
  group = 'verification'

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  shouldRunAfter test
}

check.dependsOn integrationTest

configurations {
  integrationTestImplementation.extendsFrom implementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.setCanBeResolved(true)
  integrationTestRuntimeOnly.setCanBeResolved(true)
}

/**
 * Version conflict for Kafka clients library:
 *      kafka-avro-serializer transitively depends on version 5.3.0-scc
 *      kafka-streams-test-utils depends on version 2.4.0
 * Force using 2.4.0 as otherwise Kafka streams test utils will fail. The afka-avro-serializer
 * seems to be compatible with this version.
 */

dependencies {  
  
  implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
  
  implementation group: 'io.quarkus', name: 'quarkus-bom', version: '1.11.0.Final', ext: 'pom'
  
  // Cassandra
  implementation group: 'com.datastax.oss.quarkus', name: 'cassandra-quarkus-bom', version: '1.0.0', ext: 'pom'  
  annotationProcessor group: 'com.datastax.oss', name: 'java-driver-mapper-processor', version: '4.10.0'
  implementation 'com.datastax.oss:java-driver-mapper-runtime'
  implementation 'com.datastax.oss.quarkus:cassandra-quarkus-client'
  implementation 'com.datastax.oss:java-driver-core'
  implementation 'com.datastax.oss:java-driver-query-builder'

  // Kafka
  implementation 'io.quarkus:quarkus-kafka-streams'
  implementation 'io.quarkus:quarkus-resteasy'
  implementation 'io.quarkus:quarkus-resteasy-jackson'
  implementation group: 'io.confluent', name: 'kafka-avro-serializer', version: '5.5.0'
  implementation group: 'io.confluent', name: 'kafka-streams-avro-serde', version: '5.5.0'
  implementation 'io.quarkus:quarkus-smallrye-reactive-messaging-kafka' 

  // Reactive Programming   
  implementation 'io.quarkus:quarkus-resteasy-mutiny'
  
  
  // Unit Testing
  testImplementation 'org.junit.jupiter:junit-jupiter'

  // Integration Testing  
  integrationTestImplementation 'io.quarkus:quarkus-junit5'
  integrationTestImplementation 'io.rest-assured:rest-assured'
  integrationTestImplementation 'org.mockito:mockito-junit-jupiter'
  
  integrationTestImplementation group: 'org.apache.kafka', name: 'kafka-streams-test-utils', version: '2.4.1'
   
  integrationTestImplementation 'com.datastax.oss.quarkus:cassandra-quarkus-test-framework'
}

group 'net.explorviz'
version '1.0-SNAPSHOT'

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  options.encoding = 'UTF-8'
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

sourceSets.main.java.srcDirs = ['build/generated-main-avro-java', 'src/main/java', 'build/generated/sources/annotationProcessor/java/main']

test {
  systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
