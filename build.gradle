plugins {
  id 'java'
  id 'io.quarkus'
  id 'com.adarshr.test-logger' version '2.1.0'
  id "com.commercehub.gradle.plugin.avro" version "0.21.0"
  id 'eclipse'
}

apply from: 'buildscripts/code-analysis.gradle'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://packages.confluent.io/maven/"
  }
}

sourceSets {
  integrationTest {
    java.srcDir file('src/integrationTest/java')
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
  apiTest {
    java.srcDir file('src/apiTest/java')
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

eclipse {
  classpath {
    plusConfigurations.add configurations.integrationTestRuntimeOnly
    plusConfigurations.add configurations.integrationTestImplementation
    plusConfigurations.add configurations.apiTestRuntimeOnly
    plusConfigurations.add configurations.apiTestImplementation
  }
}

task integrationTest(type: Test) {
  useJUnitPlatform{
    excludeTags 'slow'
    includeEngines 'junit-jupiter'
  }
  description = 'Runs integration tests.'
  group = 'verification'

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

task apiTest(type: Test) {
  useJUnitPlatform{
    excludeTags 'slow'
    includeEngines 'junit-jupiter'
  }
  description = 'Runs api tests.'
  group = 'verification'

  testClassesDirs = sourceSets.apiTest.output.classesDirs
  classpath = sourceSets.apiTest.runtimeClasspath
}

configurations {
  integrationTestImplementation.extendsFrom implementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly
  apiTestImplementation.extendsFrom implementation
  apiTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.setCanBeResolved(true)
  integrationTestRuntimeOnly.setCanBeResolved(true)
  apiTestImplementation.setCanBeResolved(true)
  apiTestRuntimeOnly.setCanBeResolved(true)
}

/**
 * Version conflict for Kafka clients library:
 *      kafka-avro-serializer transitively depends on version 5.3.0-scc
 *      kafka-streams-test-utils depends on version 2.4.0
 * Force using 2.4.0 as otherwise Kafka streams test utils will fail. The afka-avro-serializer
 * seems to be compatible with this version.
 */

dependencies {  
  
  implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
  
  annotationProcessor group: 'com.datastax.oss', name: 'java-driver-mapper-processor', version: '4.10.0'
  implementation group: 'com.datastax.oss', name: 'java-driver-mapper-runtime', version: '4.10.0'  
  implementation 'com.datastax.oss.quarkus:cassandra-quarkus-client'

  implementation 'io.quarkus:quarkus-kafka-streams'
  implementation 'io.quarkus:quarkus-resteasy'
  implementation 'io.quarkus:quarkus-resteasy-jackson'

  implementation 'io.opencensus:opencensus-proto:0.2.0'

  implementation group: 'io.quarkus', name: 'quarkus-smallrye-reactive-messaging-kafka'

  implementation group: 'io.confluent', name: 'kafka-avro-serializer', version: '5.5.0'
  implementation group: 'io.confluent', name: 'kafka-streams-avro-serde', version: '5.5.0'

  implementation group: 'com.datastax.oss', name: 'java-driver-core', version: '4.8.0'
  implementation group: 'com.datastax.oss', name: 'java-driver-query-builder', version: '4.8.0'
  
  implementation 'io.quarkus:quarkus-resteasy-mutiny'
  
  testImplementation(platform('org.junit:junit-bom:5.7.1'))
  testImplementation('org.junit.jupiter:junit-jupiter')

  integrationTestImplementation(platform('org.junit:junit-bom:5.7.1'))
  integrationTestImplementation('org.junit.jupiter:junit-jupiter')
  integrationTestImplementation group: 'org.apache.kafka', name: 'kafka-streams-test-utils', version: '2.4.1'
  integrationTestImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

  apiTestImplementation(platform('org.junit:junit-bom:5.7.1'))
  apiTestImplementation('org.junit.jupiter:junit-jupiter')
}

group 'net.explorviz'
version '1.0-SNAPSHOT'

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  options.encoding = 'UTF-8'
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

sourceSets.main.java.srcDirs = ['build/generated-main-avro-java', 'src/main/java', 'build/generated/sources/annotationProcessor/java/main']

test {
  systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
