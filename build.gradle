plugins {
  id 'java'
  id 'io.quarkus'
  id 'com.adarshr.test-logger' version '3.2.0'
  id 'eclipse'
}

apply from: 'buildscripts/code-analysis.gradle'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://packages.confluent.io/maven/"
  }
}

sourceSets {
  integrationTest {
    java.srcDir file('src/integrationTest/java')
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

eclipse {
  classpath {
    plusConfigurations.add configurations.integrationTestRuntimeOnly
    plusConfigurations.add configurations.integrationTestImplementation
  }
}

task integrationTest(type: Test) {
  description = 'Runs integration tests.'
  group = 'verification'

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  shouldRunAfter test
}

check.dependsOn integrationTest

configurations {
  integrationTestImplementation.extendsFrom implementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.setCanBeResolved(true)
  integrationTestRuntimeOnly.setCanBeResolved(true)
}

sourceSets.main.java.srcDirs = ['build/classes/java/quarkus-generated-sources/avdl', 'src/main/java', 'build/generated/sources/annotationProcessor/java/main']

// Fix: com.datastax.oss:* uses old, mandatory Groovy version
configurations.all {
  resolutionStrategy.force group: 'org.codehaus.groovy', name: 'groovy-json', version: '2.5.7'
  resolutionStrategy.force group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.7'
}

dependencies {  
  implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
  
  implementation 'io.quarkus:quarkus-container-image-jib'
  implementation 'io.quarkus:quarkus-micrometer-registry-prometheus'
   
  // Base    
  implementation 'io.quarkus:quarkus-scheduler'
  implementation 'io.quarkus:quarkus-resteasy-mutiny'
  
  // Cassandra
  annotationProcessor group: 'com.datastax.oss', name: 'java-driver-mapper-processor', version: '4.10.0'
  implementation 'com.datastax.oss:java-driver-mapper-runtime'
  
  // Remove version when 1.1.2 is included in Quarkus Platform, should be in 2.8.x.FINAL
  // Bug https://github.com/datastax/cassandra-quarkus/issues/203
  // Universe BOM https://mvnrepository.com/artifact/io.quarkus/quarkus-universe-bom
  implementation('com.datastax.oss.quarkus:cassandra-quarkus-client:1.0.2') {
        force = true
  }

  // Kafka
  implementation 'io.quarkus:quarkus-kafka-streams'
  implementation 'io.quarkus:quarkus-resteasy'
  implementation 'io.quarkus:quarkus-resteasy-jackson'
  implementation 'io.quarkus:quarkus-confluent-registry-avro'
  implementation group: 'io.confluent', name: 'kafka-avro-serializer', version: '7.2.1'
  implementation group: 'io.confluent', name: 'kafka-streams-avro-serde', version: '7.2.1'
  implementation 'io.quarkus:quarkus-smallrye-reactive-messaging-kafka'
  
  // Utility
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  
  // Unit Testing  
  testImplementation 'io.quarkus:quarkus-junit5'
  testImplementation 'io.quarkus:quarkus-junit5-mockito'

  testImplementation group: 'org.apache.kafka', name: 'kafka-streams-test-utils', version: '3.3.1'
  
  // Integration / Api Tests
  integrationTestImplementation 'io.rest-assured:rest-assured'
  integrationTestImplementation 'com.datastax.oss.quarkus:cassandra-quarkus-test-framework'
  integrationTestImplementation "org.testcontainers:kafka:1.17.5"

  // Integration Testing  
  integrationTestImplementation 'io.quarkus:quarkus-junit5'
  integrationTestImplementation 'org.mockito:mockito-junit-jupiter'
}

group 'net.explorviz'
version '1.0-SNAPSHOT'

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  options.encoding = 'UTF-8'
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

test {
  systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

integrationTest {
  systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
